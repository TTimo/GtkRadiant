# -*- mode: python -*-
# GtkRadiant build scripts
# TTimo <ttimo@ttimo.net>
# http://scons.org/

import os
import platform

Import( [ 'utils', 'config', 'settings', 'lib_objects' ] )

env = Environment( ENV = os.environ )
settings.SetupEnvironment( env, config['name'] )
env.Prepend( CPPPATH = [ '#tools/quake3/common', ] )
if platform.system() == "NetBSD":
    env.Append( LIBS = [ 'm', 'pthread', 'jpeg', 'png16' ] )
else:
    env.Append( LIBS = [ 'm', 'pthread', 'jpeg', 'png' ] )
proj = utils.vcxproj( os.path.join( GetLaunchDir(), 'tools/urt/tools/quake3/q3map2/q3map2_urt.vcxproj' ) )
objects = lib_objects

def keep_file( n ):
    for skip in [ 'cmdlib.c', 'imagelib.c', 'inout.c', 'vfs.c', 'mutex.c', 'polylib.c', 'scriplib.c', 'threads.c', 'unzip.c' ]:
        if ( n.find( skip ) != -1 ):
            return False
    return True

objects += list(filter( keep_file, [ os.path.join( 'tools/urt/tools/quake3/q3map2', i ) for i in proj.getSourceFiles() ] ))
q3map2_urt = env.Program( 'q3map2_urt', objects )

Return( 'q3map2_urt' )
